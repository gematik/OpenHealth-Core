// SPDX-FileCopyrightText: Copyright 2026 gematik GmbH
//
// SPDX-License-Identifier: Apache-2.0
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// *******
//
// For additional notes and disclaimer from gematik and in case of changes by gematik,
// find details in the "Readme" file.

import org.gradle.api.JavaVersion

plugins {
    kotlin("multiplatform")
    id("com.android.library")
}

val replayVectorsDir = rootDir.resolve("../test-vectors/apdu-replay").normalize()
val replayFixturesDir = layout.buildDirectory.dir("generated/replay-fixtures")
val replayFixtureMap = linkedMapOf(
    "JSONL_ESTABLISH_SECURE_CHANNEL" to "establish-secure-channel.jsonl",
    "JSONL_VERIFY_PIN" to "verify-pin.jsonl",
    "JSONL_GET_RANDOM" to "get-random.jsonl",
    "JSONL_READ_VSD" to "read-vsd.jsonl",
    "JSONL_READ_CERTS" to "read-certs.jsonl",
    "JSONL_UNLOCK_EGK_WITH_PUK" to "unlock-egk-with-puk.jsonl",
    "JSONL_CHANGE_PIN_WITH_PUK" to "change-pin-with-puk.jsonl",
)

val generateReplayFixtures = tasks.register("generateReplayFixtures") {
    val inputFiles = replayFixtureMap.values.map { replayVectorsDir.resolve(it) }
    inputs.files(inputFiles)
    outputs.dir(replayFixturesDir)

    doLast {
        val outputDir = replayFixturesDir.get().asFile
        if (!outputDir.exists() && !outputDir.mkdirs()) {
            throw GradleException("Failed to create output dir: ${outputDir.absolutePath}")
        }

        val outputFile = outputDir.resolve("ReplayFixtures.kt")
        val content = buildString {
            appendLine("// Generated by generateReplayFixtures.")
            appendLine("package de.gematik.openhealth.healthcard.replay")
            appendLine()

            replayFixtureMap.forEach { (constant, filename) ->
                val file = replayVectorsDir.resolve(filename)
                if (!file.exists()) {
                    throw GradleException("Missing APDU replay fixture: ${file.absolutePath}")
                }
                val jsonl = file.readText()
                appendLine("const val $constant: String = \"\"\"")
                append(jsonl)
                if (!jsonl.endsWith("\n")) {
                    appendLine()
                }
                appendLine("\"\"\"")
                appendLine()
            }
        }

        outputFile.writeText(content)
    }
}

kotlin {
    jvm {}
    androidTarget()
    jvmToolchain(21)

    sourceSets {
        val commonMain by getting {
            kotlin.srcDir(replayFixturesDir)
            dependencies {
                implementation(project(":healthcard"))
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0")
            }
        }
        val jvmMain by getting
        val androidMain by getting
    }
}

android {
    namespace = "de.gematik.openhealth.healthcard.testkit"
    compileSdk = 36
    defaultConfig {
        minSdk = 24
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile>().configureEach {
    dependsOn(generateReplayFixtures)
}
